volatile unsigned long count = 0;
unsigned long lastUpdate = 0;
unsigned long secondsElapsed = 0;

void setup() {
  Serial.begin(115200);
  pinMode(5, INPUT_PULLUP);

  // Enable pin change interrupt on PCINT21 (pin 5 = PD5)
  PCICR |= (1 << PCIE2);     // Enable PORTD pin change interrupts
  PCMSK2 |= (1 << PCINT21);  // Enable interrupt for pin 5

  lastUpdate = millis();
}

ISR(PCINT2_vect) {
  static bool lastState = HIGH;
  bool currentState = digitalRead(5);

  if (lastState == HIGH && currentState == LOW) {
    count++;
  }

  lastState = currentState;
}

void loop() {
  if (millis() - lastUpdate >= 1000) {  // every 1 second
    lastUpdate += 1000;
    secondsElapsed++;

    noInterrupts();
    unsigned long ticksThisSecond = count;
    count = 0;
    interrupts();

    Serial.print("Second ");
    Serial.print(secondsElapsed);
    Serial.print(": ");
    Serial.print(ticksThisSecond);
    Serial.println(" count");
  }
}
